color_nr equ 8

;-----------;
;-  WAIT   -;
;-----------;
.proc wait
 lda:cmp:req cloc
 dex
 bne wait
 rts
.endp


;-----------;
;-  FADE   -;
;-----------;
.proc fade
 stx adr+1
 sty adr+2

 ldx #6
 jsr wait
 
 lda #16
 sta licz
fad
 ldx #2
 jsr wait

adr jsr $ffff

 jsr colors

 dec licz
 bne fad
 rts

licz	.BYTE
.endp


;-----------;
;- FADE IN -;
;-----------;
.proc fade_in

 ldx <fade
 ldy >fade
 jmp intro.fade

fade

 lda <intro.tab.l_tcol
 sta adr0+1
 lda <intro.tab.h_tcol
 sta adr1+1
 lda <intro.tab.t_satur
 sta adr2+1
 lda <intro.tab.t_color
 sta adr3+1

 lda >intro.tab.l_tcol
 sta adr0+2
 lda >intro.tab.h_tcol
 sta adr1+2
 lda >intro.tab.t_satur
 sta adr2+2
 lda >intro.tab.t_color
 sta adr3+2

 ldx #0
 stx count

fade_l
adr0 lda $ffff,x
 sta ftmp
adr1 lda $ffff,x
 sta ftmp+1

 ldy #1
 lda (ftmp),y
 tay

 and #$f0
adr3 cmp $ffff,x
 beq skp1

 tya
 clc
 adc #$10
 tay

skp1

 tya
 and #$0f
adr2 cmp $ffff,x
 beq skp0

 iny

skp0

 tya
 ldy #1
 sta (ftmp),y

 inx
 bne skp
 inc count

 inc adr0+2
 inc adr1+2
 inc adr2+2
 inc adr3+2

skp

 cpx #color_nr
 bne fade_l
 rts

count	.BYTE
.endp


;------------;
;- FADE OUT -;
;------------;
.proc fade_out

 ldx <fade
 ldy >fade
 jmp intro.fade

fade

 lda <intro.tab.l_tcol
 sta adr0+1
 lda <intro.tab.h_tcol
 sta adr1+1

 lda >intro.tab.l_tcol
 sta adr0+2
 lda >intro.tab.h_tcol
 sta adr1+2

 ldx #0
 stx count

fade_l
adr0 lda $ffff,x
 sta ftmp
adr1 lda $ffff,x
 sta ftmp+1

 ldy #1
 lda (ftmp),y
 tay

 and #$0f
 beq skp0

 dey

skp0

 tya
 and #$f0
 beq skp1

 tya
 sec
 sbc #$10
 tay

skp1
 tya

 ldy #1
 sta (ftmp),y

 inx
 bne skp
 inc count

 inc adr0+2
 inc adr1+2

skp

 cpx #color_nr
 bne fade_l
 rts

count	.BYTE
.endp


.proc tab
l_tcol
 dta l(c0,c1,c2,c3,c4,c5,c6,c7)

h_tcol
 dta h(c0,c1,c2,c3,c4,c5,c6,c7)
 
t_satur dta $00,$04,$0a,$08,$04,$08,$08,$0c
t_color dta $00,$50,$80,$60,$30,$f0,$c0,$00
.endp
